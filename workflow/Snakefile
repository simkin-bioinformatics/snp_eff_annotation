# load configuration
# -----------------------------------------------------
configfile: "config/config.yaml
# target rules
# -----------------------------------------------------
rule all:
	input:
		coverage_AA=f"{config['output_folder']}/AA_tables/coverage_AA_table.csv",
		reference_AA=f"{config['output_folder']}/AA_tables/reference_AA_table.csv",
		alternate_AA=f"{config['output_folder']}/AA_tables/alternate_AA_table.csv",
	default_target: True

rule prepare_snp_eff_database:
	'''
	rearranges genome fasta and genome gff files for snp_eff. Specifically:
	1. makes a 'data' folder in the snpeff folder
	2. makes a subfolder in 'data' with your desired genome name
	3. copies your gff and genome files to the newly created genome folder and
	renames your gff file as genes.gff and your genome as sequences.fa
	4. Adds a line to the config file underneath the data.dir line and puts the
	name of your genome folder plus .genome, followed by a description,
	formatted like this: your_desired_name.genome : your_description
	'''
	input:
		snp_eff_folder=config['snp_eff_folder'],
		genome_fasta=config['genome_fasta'],
		genome_gff=config['genome_gff']
	params:
		database=config['genome_database_name'],
		description=config['database_description'],
		output_folder=config['output_folder']
	output:
		genome_fasta=f"{config['output_folder']}/snpeff/data/{config['genome_database_name']}/sequences.fa",
		genome_gff=f"{config['output_folder']}/snpeff/data/{config['genome_database_name']}/genes.gff",
		snp_eff_config=f"{config['output_folder']}/snpeff/snpEff.config",
		snp_eff_jar=f"{config['output_folder']}/snpeff/snpEff.jar"
	shell:
		"""
		cp -r {input.snp_eff_folder}/* {params.output_folder}/snpeff/
		sed -i '/data.dir = .\/data/a {params.database}.genome : {params.description}' {output.snp_eff_config}
		cp -r {input.genome_fasta} {output.genome_fasta}
		cp -r {input.genome_gff} {output.genome_gff}
		"""


rule build_snp_eff:
	input:
		genome_fasta=f"{config['output_folder']}/snpeff/data/{config['genome_database_name']}/sequences.fa",
		genome_gff=f"{config['output_folder']}/snpeff/data/{config['genome_database_name']}/genes.gff",
	params:
		snp_eff_folder=f"{config['output_folder']}/snpeff",
		database_name=config['genome_database_name']
	output:
		snp_eff_database=f"{config['output_folder']}/snpeff/data/{config['genome_database_name']}/sequence.bin"
	shell:
		'java -Xmx8g -jar {params.snp_eff_folder}/snpEff.jar build -c {params.snp_eff_folder}/snpEff.config -noCheckCds -noCheckProtein {params.database_name}'

rule run_snp_eff:
	input:
		snp_eff_jar=f"{config['output_folder']}/snpeff/snpEff.jar",
		merged_vcf=f"{config['merged_vcf']}",
		snp_eff_database=f"{config['output_folder']}/snpeff/data/{config['genome_database_name']}/sequence.bin"
	params:
		database_name=config['genome_database_name'],
		snp_eff_output_folder = f"{config['output_folder']}/snp_Eff_output"
	output:
		snp_eff_stats=f"{config['output_folder']}/snp_Eff_output/snpEff_summary.html",
		snp_eff_vcf=f"{config['output_folder']}/snp_Eff_output/annotated_variants.vcf"
	shell:
		'''
		java -Xmx10g -jar {input.snp_eff_jar} {params.database_name} \
		{input.merged_vcf} \
		-stats {output.snp_eff_stats} >{output.snp_eff_vcf}
		'''

rule parse_snp_eff:
	input:
		snp_eff_vcf=f"{config['output_folder']}/snp_Eff_output/annotated_variants.vcf",
		genome_gff=config['genome_gff'],
		targets_tsv=config['targets_tsv']
	output:
		coverage_AA=f"{config['output_folder']}/AA_tables/coverage_AA_table.csv",
		reference_AA=f"{config['output_folder']}/AA_tables/reference_AA_table.csv",
		alternate_AA=f"{config['output_folder']}/AA_tables/alternate_AA_table.csv",
	script:
		'scripts/snpEff_parser.py'
